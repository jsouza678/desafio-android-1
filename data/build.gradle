import com.picpay.buildSrc.Dependencies
import com.picpay.buildSrc.Versions

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdk

    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs = freeCompilerArgs + "-Xallow-result-return-type"
    }

    if (project.hasProperty('kapt')) {
        // Reference for 'kapt' DSL: https://kotlinlang.org/docs/reference/kapt.html#java-compiler-options
        kapt {
            // we expect this closure to run over a org.jetbrains.kotlin.gradle.plugin.KaptExtension
            javacOptions {
                option("-source", "8")
                option("-target", "8")
            }
        }
    }
}

dependencies {
    Dependencies.Modules.dataModules.forEach { module ->
        api project (module)
    }

    Dependencies.Core.list.forEach { dependency ->
        implementation dependency
    }

    Dependencies.Network.list.forEach { dependency ->
        api dependency
    }

    Dependencies.DI.list.forEach { dependency ->
        implementation dependency
    }

    kapt Dependencies.Core.roomCompiler

    Dependencies.Test.testList.forEach { testDependency ->
        testImplementation testDependency
    }
}
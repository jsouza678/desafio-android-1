import com.picpay.buildSrc.Versions
import com.picpay.buildSrc.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdk

    defaultConfig {
        applicationId "com.picpay.desafio.android"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def API_BASE_URL = "API_BASE_URL"

    buildTypes {
        debug {
            debuggable false
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "connectivity"

    productFlavors {
        mock {
            dimension 'connectivity'
            buildConfigField "String", API_BASE_URL, '"https://609a908e0f5a13001721b74e.mockapi.io/picpay/api/"'
        }

        remote {
            dimension 'connectivity'
            buildConfigField "String", API_BASE_URL, '"https://609a908e0f5a13001721b74e.mockapi.io/picpay/api/"'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs = freeCompilerArgs + "-Xallow-result-return-type"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    Dependencies.Modules.appModules.forEach { module ->
        implementation project (module)
    }

    Dependencies.Core.list.forEach { dependency ->
        implementation dependency
    }

    kapt Dependencies.Core.roomCompiler

    Dependencies.UI.list.forEach { dependency ->
        implementation dependency
    }

    Dependencies.DI.list.forEach { dependency ->
        implementation dependency
    }

    Dependencies.Test.testList.forEach { testDependency ->
        testImplementation testDependency
    }

    Dependencies.Test.androidTestList.forEach { testDependency ->
        androidTestImplementation testDependency
    }
}

import com.picpay.buildSrc.Versions
import com.picpay.buildSrc.Dependencies

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.picpay.desafio.android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {}

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "connectivity"

    productFlavors {
        mock {
            dimension 'connectivity'
        }

        remote {
            dimension 'connectivity'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs = freeCompilerArgs + "-Xallow-result-return-type"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    Dependencies.Core.list.forEach { dependency ->
        implementation dependency
    }

    Dependencies.UI.list.forEach { dependency ->
        implementation dependency
    }

    Dependencies.Network.list.forEach { dependency ->
        implementation dependency
    }

    Dependencies.DI.list.forEach { dependency ->
        implementation dependency
    }

    kapt Dependencies.Core.roomCompiler

    Dependencies.Test.testList.forEach { testDependency ->
        testImplementation testDependency
    }

    Dependencies.Test.androidTestList.forEach { testDependency ->
        androidTestImplementation testDependency
    }
}
